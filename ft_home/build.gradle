apply plugin: 'com.android.application'
apply plugin: 'com.alibaba.arouter'

android {
  compileSdkVersion rootProject.android.compileSdkVersion

  defaultConfig {
    applicationId 'com.imooc.ft_home'
    minSdkVersion rootProject.android.minSdkVersion
    targetSdkVersion rootProject.android.targetSdkVersion
    versionCode 1
    versionName "1.0"
    multiDexEnabled false // 不需要分包了
    //ARouter配置
    javaCompileOptions {
      annotationProcessorOptions {
        arguments = [AROUTER_MODULE_NAME: project.getName(), AROUTER_GENERATE_DOC: "enable"]
      }
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  lintOptions {
    abortOnError false
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation rootProject.depsLibs.appcompact
  implementation rootProject.depsLibs.recyclerview
  //eventbus类库依赖
  implementation rootProject.depsLibs.eventbus
  //gson解析库
  implementation rootProject.depsLibs.gson
  //arouter库
  implementation(rootProject.depsLibs.arouterapi) {
    exclude group: 'com.android.support'
  }

  implementation project(':lib_base')
  //视频播放器组件
  implementation project(':lib_video')
  //app下载更新库
  implementation project(':lib_update')
  //网络请求库
  implementation project(':lib_network')
  //基础UI库
  implementation project(':lib_commin_ui')
  //图片加载库
  implementation project(':lib_image_loader')

  annotationProcessor rootProject.depsLibs.aroutercompiler
}

//必须所有的库都去除，任何一个库引入了fragment，都会报错
configurations.all {
  exclude group: 'androidx.fragment', module: 'fragment'
}

//引入插件
apply plugin: 'replugin-plugin-gradle'
//插件配置
repluginPluginConfig{
  //当前插件的名称，其他插件通过这个名称来访问当前插件
  pluginName = 'ft_home'
  //宿主applicationid，当前插件被集成到哪个宿主工程中去，比如多渠道打包的时候需要修改这个applicationid
  hostApplicationId = 'com.imooc.imooc_voice'
  //宿主工程启动的activity
  hostAppLauncherActivity = 'com.imooc.ft_loading.view.loading.LoadingActivity'
}
//添加依赖
dependencies {
  implementation 'com.qihoo360.replugin:replugin-plugin-lib:2.3.3'
  //这个jar就是从Support-fragment中提取出来的
  compileOnly files('libs/fragment.jar')
}
